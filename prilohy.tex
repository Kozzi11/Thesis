\chapter{\xen příprava balíčku}
\section{PKGBUILD}
\label{xenpkgbuild}
\begin{lstlisting}
pkgname=xen-rc
pkgver=23276
pkgrel=1
pkgdesc="Xen 4.1.3 rc (hypervisor tools and doc) HG"
arch=(i686 x86_64)
url="http://xen.org/"
license="GPL"
depends=('bzip2' 'iproute' 'bridge-utils' 'python2' 'sdl'
         'zlib' 'e2fsprogs' 'bin86' 'pkgconfig' 'gnutls'
         'lzo2' 'glibc')
makedepends=('dev86' 'mercurial' 'git' 'ghostscript')
conflicts=('xen' 'xen3' 'xen4' 'xen-hv-tools' 'libxen4')
provides=('xen')
source=(archinit.patch texi2html.patch 09_xen)

md5sums=('d3ab9bbae472e613a04dc8e62377ed93'
         'c94602f1feaa5d968db1e9f640dfd2a5'
         '3a3240e1edde3a8e295f928be52dbde4')
			   
_hgroot="http://xenbits.xensource.com/"
_hgrepo="xen-4.1-testing.hg"

build() {

  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  patch -p1 -F99 -i ../archinit.patch
  patch -p1 -i ../texi2html.patch
  unset CFLAGS LDFLAGS

  make PYTHON=python2 DESTDIR=$pkgdir  xen
  make PYTHON=python2 DESTDIR=$pkgdir  tools  
    
}

package() {

  cd "$srcdir/$_hgrepo-build"
  unset CFLAGS LDFLAGS
  make PYTHON=python2 DESTDIR=$pkgdir  install-xen
  make PYTHON=python2 DESTDIR=$pkgdir  install-tools  
  
  sed -i 's#XENDOM_CONFIG=/etc/sysconfig/xendomains#XENDOM_CONFIG=/etc/conf.d/xendomains#' $pkgdir/etc/init.d/xendomains
  sed -i "s#touch /var/lock/subsys/xend#mkdir -p /var/lock/subsys\n     touch /var/lock/subsys/xend#" $pkgdir/etc/init.d/xend

  [ -d $pkgdir/usr/lib64 ] && ( cd $pkgdir/usr && cp -R lib64/* lib/ && rm -R lib64 )
  ( cd $pkgdir/etc && mv init.d rc.d ) || return 1
  rm -f $pkgdir/usr/share/man/man1/qemu-img.1* \
       $pkgdir/usr/share/man/man1/qemu.1*
  # First experiment to generate grub2.cfg entry
  mkdir -p $pkgdir/etc/grub.d
  chmod +x $srcdir/09_xen
  cp $srcdir/09_xen  $pkgdir/etc/grub.d

  ############ kill unwanted stuff ############
  # stubdom: newlib
  rm -rf $pkgdir/usr/*-xen-elf

  # hypervisor symlinks
  rm -rf $pkgdir/boot/xen-4.1.gz
  rm -rf $pkgdir/boot/xen-4.gz
  rm -rf $pkgdir/boot/xen.gz

  # silly doc dir fun
  rm -fr $pkgdir/usr/share/doc/xen
  rm -rf $pkgdir/usr/share/doc/qemu

  # Pointless helper
  rm -f $pkgdir/usr/sbin/xen-python-path

  # qemu stuff (unused or available from upstream)
  rm -rf $pkgdir/usr/share/xen/man
  rm -rf $pkgdir/usr/bin/qemu-*-xen
  for file in bios.bin openbios-sparc32 openbios-sparc64 ppc_rom.bin \
         pxe-e1000.bin pxe-ne2k_pci.bin pxe-pcnet.bin pxe-rtl8139.bin \
         vgabios.bin vgabios-cirrus.bin video.x openbios-ppc bamboo.dtb
  do
        rm -f $pkgdir/usr/share/xen/qemu/$file
  done

  # adhere to Static Library Packaging Guidelines
  rm -rf $pkgdir/usr/lib/*.a 	
}
\end{lstlisting}

\chapter{LXC konfigurační soubory}
\section{lxc-arch.conf}
\label{lxcarch}
\begin{verbatim}
  lxc.utsname = ArchV
  lxc.network.type = veth
  lxc.network.flags = up
  lxc.network.link = br0
  lxc.network.ipv4 = 192.168.100.112/24
  lxc.rootfs = /vservers/ArchV
  lxc.cgroup.cpuset.cpus = 0
\end{verbatim}
\section{lxc-fedora.conf}
\label{lxcfedora}
\begin{verbatim}
  lxc.utsname = FedoraV
  lxc.network.type = veth
  lxc.network.flags = up
  lxc.network.link = br0
  lxc.network.ipv4 = 192.168.100.113/24
  lxc.rootfs = /vservers/FedoraV
  lxc.cgroup.cpuset.cpus = 1
\end{verbatim}

%\chapter{Obsah CD}
%\chapter{Manual}
%\chapter{Konfigrační soubor}
%\chapter{RelaxNG Schéma konfiguračního soboru}
%\chapter{Plakat}
